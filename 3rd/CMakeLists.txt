set(third_party_folder "ThirdParty")
set(THIRD_PARTY_ROOT ${CMAKE_CURRENT_SOURCE_DIR})

if(NOT TARGET glad)
    set(GLAD_ROOT_DIR ${THIRD_PARTY_ROOT}/glad)
    add_library(glad STATIC ${GLAD_ROOT_DIR}/src/glad.c)
    add_library(glad::glad ALIAS glad)
    target_include_directories(glad PUBLIC ${GLAD_ROOT_DIR}/include)
    set_target_properties(glad PROPERTIES FOLDER ${third_party_folder}/glad)
endif()

if(NOT TARGET glfw)
    option(GLFW_BUILD_EXAMPLES "" OFF)
    option(GLFW_BUILD_TESTS "" OFF)
    option(GLFW_BUILD_DOCS "" OFF)
    option(GLFW_INSTALL "" OFF)
    add_subdirectory(glfw)
    set_target_properties(glfw PROPERTIES FOLDER ${third_party_folder}/glfw)
    set_target_properties(update_mappings PROPERTIES FOLDER ${third_party_folder}/glfw)
endif()

if(NOT TARGET glm)
    add_subdirectory(glm)
    set_target_properties(glm PROPERTIES FOLDER ${third_party_folder}/glm)
endif()

if(NOT TARGET stb)
    add_library(stb INTERFACE)
    target_include_directories(stb INTERFACE ${THIRD_PARTY_ROOT}/stb)
    set_target_properties(stb PROPERTIES FOLDER ${third_party_folder}/stb)
endif()

if(NOT TARGET imgui)
    include(imgui.cmake)
    set_target_properties(imgui PROPERTIES FOLDER ${third_party_folder}/imgui)
endif()
